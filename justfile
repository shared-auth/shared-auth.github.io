set shell := ["bash", "-euo", "pipefail", "-c"]
set dotenv-load := false

_default:
    @just --list --unsorted

bootstrap:
    @mkdir -p env/enc env/dec
    @chmod 700 env/dec
    @ores-sops install-hooks
    @ores-sops verify

seed name:
    #!/usr/bin/env bash
    set -euo pipefail
    case '{{ name }}' in dev|prod) ;; *) echo "name must be dev or prod" >&2; exit 2 ;; esac
    mkdir -p env/dec && chmod 700 env/dec
    target="env/dec/{{ name }}.env"
    test ! -e "$target" || { echo "refusing to overwrite $target" >&2; exit 1; }
    umask 077; cp .env.example "$target"; chmod 600 "$target"

# Normal Pages-style build. PUBLIC_DASHBOARD_URL is public metadata and does not require decryption.
build-public:
    @npm ci --no-audit --no-fund
    @npm run build
    @npm test

# Optional local profile for explicitly reviewed future non-secret variants.
build-local name="dev":
    @sops exec-env --input-type dotenv env/enc/{{ name }}.env.enc 'npm ci --no-audit --no-fund && npm run build && npm test'

use name:
    @mkdir -p env/dec && chmod 700 env/dec
    @ores-sops use {{ name }}
status:
    @ores-sops status
edit name:
    @ores-sops edit {{ name }}
encrypt name:
    @mkdir -p env/dec && chmod 700 env/dec
    @ores-sops encrypt {{ name }}
diff name:
    @ores-sops diff {{ name }}
refresh:
    @mkdir -p env/dec && chmod 700 env/dec
    @ores-sops refresh
lock:
    @ores-sops lock
verify:
    @bash scripts/check-env-policy.sh
    @ores-sops verify
verify-release-policy name="prod":
    @python3 scripts/verify-sops-release-policy.py .sops.yaml {{ name }}
